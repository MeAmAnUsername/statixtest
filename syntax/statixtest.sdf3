module statixtest

imports
  
  Common

context-free start-symbols
  
  Start

context-free sorts

  Start
  // todo

context-free syntax
  
  Start = ScopeGraph
  Start.StxResult = [
  substitution
    [{Substitution "\n"}*]
  
  analysis
    [ScopeGraph]

  errors
    [REST]]

  Substitution.Substitution = [[ID] |-> [NONEWLINE]]
  
  ScopeGraph.ScopeGraph = [
    scope graph
      [{Scope "\n"}*]]
  
  Scope.Scope = [[SCOPEID] {
    [{ScopeElement "\n"}*]
  }]
  
  ScopeElement.ScopeElement = [[ID] {
    [{NodeDeclaration "\n"}*]
  }]

  NodeDeclaration.NodeDeclaration = [[PATH]![ID] : [{ATerm "\n"}+]]

  ATerm.String = [[UNQUOTED_STRING]] {avoid}
  ATerm.Int = [[INT]]
  ATerm.ScopeRef = [[SCOPEID]]
  ATerm.List = <[<{ATerm ", "}*>]>
  ATerm.Tuple = [([{ATerm ", "}*])]
  ATerm.Constructor = [[ID]([{ATerm ", "}*])]

lexical sorts

  NONEWLINE
  SCOPEID
  PATH
  PATHID
  UNQUOTED_STRING
  REST

lexical syntax

  NONEWLINE = (~[\r\n])+
  SCOPEID = "#" PATH? "-" ID "-" INT
  PATH = PATHID ("/" PATHID)*
  PATHID = [A-Za-z0-9\_\-\.]+
  UNQUOTED_STRING = ~[\,\)\}\n\/]+
  REST = (~[])+ EOF

lexical restrictions

  UNQUOTED_STRING -/- ~[\,\)\}\n\/]