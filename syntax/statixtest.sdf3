module statixtest

imports
  
  Common

context-free start-symbols
  
  Start

context-free sorts

  Start
  Substitution
  Node
  NodeDeclaration
  Decl
  NodeId
  ATerm

context-free syntax
  
  Start.Program =
  [substitution
    [{Substitution "\n"}*]
  
  analysis
    scope graph
      [{Node "\n"}*]

  errors
  [REST]]

  Substitution.Substitution = [[ID] |-> [NONEWLINE]]
  
  Node.Declaration = [[DECLID] : [ATerm] {
  }] {prefer}
  Node.Scope = [[SCOPEID] : [SCOPEID] {
    [{NodeDeclaration "\n"}*]
  }]
  Node.PrettyScope = [[SCOPEID] {
    [{NodeDeclaration "\n"}*]
  }]

  NodeDeclaration.NodeDeclaration = [[PATH]![ID] : [{Decl "\n"}+]]
  
  Decl.Ref = [[NodeId]] {prefer}
  Decl.ATerm = [[ATerm]]

  NodeId.DeclId = [[DECLID]] {prefer}
  NodeId.ScopeId = [[SCOPEID]]

  ATerm.String = [[STRING]]
  ATerm.Int = [[INT]]
  ATerm.ScopeRef = [[SCOPEID]]
  ATerm.List = <[<{ATerm ", "}*>]>
  ATerm.Tuple = [([{ATerm ", "}*])]
  ATerm.Constructor = [[ID]([{ATerm ", "}*])]

lexical sorts

  NONEWLINE
  DECLID
  SCOPEID
  PATH
  REST

lexical syntax

  NONEWLINE = (~[\r\n])+
  DECLID = "#-d_" INT "-" INT
  SCOPEID = "#-" ID "-" INT // todo: make non-ambiguous with DECLID
  PATH = ID ("/" ID)*
  REST = (~[])+ EOF
