module statixtest

imports
  
  Common

context-free start-symbols
  
  Start

context-free sorts

  Start
  Substitution
  ScopeGraph
  Scope
  ScopeElement
  Declaration
  IdOptionalPath
  ATerm
  ScopeRef

context-free syntax
  
  Start = ScopeGraph
  Start.StxResult = [
  substitution
    [{Substitution "\n"}*]
  
  analysis
    [ScopeGraph]

  errors
    [REST]]

  Substitution.Substitution = [[ID] |-> [NONEWLINE]]
  
  ScopeGraph.ScopeGraph = [
    scope graph
      [{Scope "\n"}*]]
  
  Scope.Scope = [[SCOPEID] {
    [{ScopeElement "\n"}*]
  }]
  
  ScopeElement.ScopeElement = [[ID] {
    [{Declaration "\n"}*]
  }]

  Declaration.Declaration = [[IdOptionalPath] : [{ATerm "\n"}+]]

  IdOptionalPath.NoPath = [[ID]]
  IdOptionalPath.WithPath = [[PATH]![ID]]

  ATerm.String = [[STRING]]
  ATerm.Int = [[INT]]
  ATerm.ScopeRef = [[ScopeRef]]
  ATerm.List = <[<{ATerm ", "}*>]>
  ATerm.Tuple = [([{ATerm ", "}*])]
  ATerm.Constructor = [[ID]([{ATerm ", "}*])]

  ScopeRef.Resolved = [[SCOPEID]]
  ScopeRef.Unresolved = [[UNRESOLVED_SCOPEID]]

lexical sorts

  NONEWLINE
  SCOPEID
  UNRESOLVED_SCOPEID
  PATH
  PATHID
  REST

lexical syntax

  NONEWLINE = (~[\r\n])+
  SCOPEID            = "#" PATH? "-" ID "-" INT
  UNRESOLVED_SCOPEID = "?" PATH? "-" ID "-" INT
  PATH = PATHID ("/" PATHID)*
  PATHID = [A-Za-z0-9\_\-\.]+
  REST = (~[])+ EOF
